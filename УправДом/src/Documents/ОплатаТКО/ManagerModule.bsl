
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс


// Переопределяет настройки печати для объекта.
    //
    // Параметры:
    //  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
    //
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
 	
	Настройки.ПриДобавленииКомандПечати = Истина;
           
КонецПроцедуры


 // Заполняет список команд печати.
    // 
    // Параметры:
    //   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
    //
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
        
		Команда = КомандыПечати.Добавить();		
//		Команда.МенеджерПечати = "Документ.ОплатаГазоснабжения"; //Можно не указывать т.к. Мен-р сам документ.
		Команда.Идентификатор = "ПечатьТКО";
		Команда.Представление = НСтр("ru = 'Печать платежа'");
		Команда.ПроверкаПроведенияПередПечатью = Истина;
		Команда.Порядок = 10;  
		
          
КонецПроцедуры


// Формирует печатные формы.
//
// Параметры:
// Массив - МассивОбъектов - ссылки на объекты, которые нужно распечатать;
// Структура - ПараметрыПечати  - дополнительные настройки печати;
// ТаблицаЗначений - КоллекцияПечатныхФорм - сформированные табличные документы (выходной параметр)
// СписокЗначений - ОбъектыПечати - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
// Структура - ПараметрыВывода - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ПечатьТКО");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТКО(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать оплаты ТКО'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ОплатаТКО.ПФ_MXL_ПечатьТКО";	    
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	Массив - МассивОбъектов ,  ссылки на объекты, которые нужно распечатать;
//  СписокЗначений - ОбъектыПечати ,  значение  ссылка на объект;
//  представление  имя области, в которой был выведен объект (выходной параметр);
//  
// Возвращаемое значение:
// ТабличныйДокумент - ТабличныйДокумент
Функция ПечатьТКО(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПечатьТКО";
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОплатаТКО.ПФ_MXL_ПечатьТКО");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОплатаТКО.Ссылка КАК Ссылка,
		|	ОплатаТКО.Номер КАК Номер,
		|	ОплатаТКО.Дата КАК Дата,
		|	ОплатаТКО.ВидПлатежа КАК ВидПлатежа,
		|	ОплатаТКО.ОплачиваемыйПериодСлужебный КАК ПериодОплСлужебный,
		|	ОплатаТКО.Поставщик КАК Поставщик,
		|	ОплатаТКО.Поставщик.ИНН КАК ПоставщикИНН,
		|	ОплатаТКО.Поставщик.Телефон КАК ПоставщикТелефон,
		|	ОплатаТКО.Поставщик.ЭлектроннаяПочта КАК ПоставщикЭлектроннаяПочта,
		|	ОплатаТКО.Поставщик.Сайт КАК ПоставщикСайт,
		|	ОплатаТКО.Поставщик.ЛицевойСчет КАК ПоставщикЛицевойСчет,
		|	ОплатаТКО.Комментарий КАК Комментарий,
		|	ОплатаТКО.ДанныеПлатежа.(
		|		Тариф КАК Тариф,
		|		СуммаПлатежа КАК СуммаПлатежа,
		|		Норматив КАК Норматив,
		|		КоличествоЧеловек КАК КоличествоЧеловек) КАК ДанныеПлатежа
		|ИЗ
		|	Документ.ОплатаТКО КАК ОплатаТКО
		|ГДЕ
		|	ОплатаТКО.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();

	РаботаСДокументамиПечатьСерверУД.ВыводНаПечатьОплаты(ТабличныйДокумент, Макет, РезультатЗапроса, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
	
КонецФункции


#КонецОбласти

#КонецЕсли
