 
#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
//	НАЧАЛО. Создание заданных групп в ПрисоединенныеФайлы	
	// Если это новый документ, группы создавать не нужно.
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// Готовим параметры для вызова универсальной процедуры
	Владелец = Объект.Ссылка;
	ИмяСправочника = "ОплатаТКОПрисоединенныеФайлы"; // <-- Имя справочника для этого документа
	
	СписокГрупп = Новый Массив;
	СписокГрупп.Добавить("Квитанции");
	СписокГрупп.Добавить("Чеки");
	
	// Вызываем наш универсальный метод из общего модуля
	РаботаСПрисоединеннымиФайламиСерверУД.СоздатьСтандартныеГруппыПрисоединенныхФайлов(Владелец, ИмяСправочника, СписокГрупп);
//	Конец. Создание заданных групп в ПрисоединенныеФайлы
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементов();
	
	ПриОткрытииНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
		
КонецПроцедуры


&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УстановитьДоступностьЭлементов();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура ОплачиваемыйМесяцПриИзменении(Элемент)
	
	// Собираем данные 
	ВыбранныйМесяц = Объект.ОплачиваемыйМесяц;
	ВыбранныйГод = Объект.ГодПлатежа;
	//запоняем рквизит ОплачиваемыйПериодСлужебный
	СлужебнаяДата = РаботаСДокументамиСерверУД.РассчитатьСлужебнуюДату(ВыбранныйМесяц, ВыбранныйГод);
	Объект.ОплачиваемыйПериодСлужебный = СлужебнаяДата;
	
	Объект.ДанныеПлатежа.Очистить();
	
КонецПроцедуры


&НаКлиенте
Процедура ГодПлатежаПриИзменении(Элемент)
	
	ВыбранныйМесяц = Объект.ОплачиваемыйМесяц;
	ВыбранныйГод = Объект.ГодПлатежа;
	
	СлужебнаяДата = РаботаСДокументамиСерверУД.РассчитатьСлужебнуюДату(ВыбранныйМесяц, ВыбранныйГод);
	Объект.ОплачиваемыйПериодСлужебный = СлужебнаяДата;
	
	Объект.ДанныеПлатежа.Очистить();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура ЗаполнитьДанныеПлатежа(Команда)
	
	Если Объект.ОплачиваемыйМесяц.Пустая() Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан оплачиваемый период. Заполнение невозможно.";
		Сообщение.Поле = "Объект.ОплачиваемыйМесяц";
		Сообщение.Сообщить();
		Возврат;
	ИначеЕсли Объект.ГодПлатежа = 0 Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан год платежа. Заполнение невозможно.";
		Сообщение.Поле = "Объект.ГодПлатежа";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;

	ЗаполнитьДанныеПлатежаНаСервере();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	Элементы.ОплачиваемыйМесяц.ТолькоПросмотр = Объект.Проведен;
	Элементы.ГодПлатежа.ТолькоПросмотр = Объект.Проведен;
	Элементы.Поставщик.ТолькоПросмотр = Объект.Проведен;
	Элементы.ДанныеПлатежа.ТолькоПросмотр = Объект.Проведен;
	Элементы.ДанныеПлатежаЗаполнитьДанныеПлатежа.Доступность = НЕ Объект.Проведен;

КонецПроцедуры


&НаСервере
Процедура ПриОткрытииНаСервере()
	
	//заполняем реквизиты формы документа
	Если Параметры.Ключ.Пустая()Тогда
		Объект.ГодПлатежа = Год(ТекущаяДатаСеанса());
	КонецЕсли;
	
	ДанныеЗаполнения = РаботаСДокументамиСерверУД.ПолучитьДанныеДляЗаполненияШапки(Объект.ВидПлатежа);
	
	Если ДанныеЗаполнения <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	Иначе 
	//	возврат не делаем так как для логики эты данные не нужны, это просто реквизиты формы	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка заполнения шапки документа. Заполните справочник ""Виды платежей"".";
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДанныеПлатежаНаСервере()
	
	
	// 1. Получаем все необходимые данные
	ТекущийНорматив = РегистрыСведений.НормативыКоммунальныхУслуг.ПолучитьАктуальныйНорматив(Объект.ВидПлатежа,
								 Объект.ОплачиваемыйПериодСлужебный);
	
	ТекущийТариф = РегистрыСведений.ТарифыКоммунальныхУслуг.ПолучитьАктуальныйТариф(Объект.ВидПлатежа,
								 Объект.ОплачиваемыйПериодСлужебный);
								 
	КоличествоЧеловек = Справочники.СоставСемьи.ПолучитьКоличествоЧеловекУчитыватьВПлатежах();
	
	
	// 2. Проверяем, все ли данные в наличии для заполнения
	Если ТекущийНорматив = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не найден актуальный норматив для расчета.";
		Сообщение.Поле = "Объект.ОплачиваемыйМесяц"; 
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ТекущийТариф = 0 Тогда
		СообщениеОТарифе = Новый СообщениеПользователю;
		СообщениеОТарифе.Текст = "Не найден актуальный тариф для расчета.";
		СообщениеОТарифе.Поле = "Объект.ОплачиваемыйМесяц"; 
		СообщениеОТарифе.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если КоличествоЧеловек = 0 Тогда
		СообщениеОКоличестве = Новый СообщениеПользователю;
		СообщениеОКоличестве.Текст = "Не найдено количество человек, учитываемых в платежах. Проверьте справочник Состав семьи.";
		СообщениеОКоличестве.Поле = "Объект.ДанныеПлатежа"; 
		СообщениеОКоличестве.Сообщить();
		Возврат;
	КонецЕсли;
	
	
	// 3. Рассчитываем  сумму
	СуммаПлатежа = КоличествоЧеловек * ТекущийТариф * ТекущийНорматив;
	
	
	// 4. Готовим табличную часть (предполагаем, что в документе должна быть только одна строка)
	Объект.ДанныеПлатежа.Очистить();
	
	// 5. Создаем новую строку и заполняем ее
	НоваяСтрока = Объект.ДанныеПлатежа.Добавить();
	
	НоваяСтрока.Норматив = ТекущийНорматив;
	НоваяСтрока.Тариф = ТекущийТариф;
	НоваяСтрока.КоличествоЧеловек = КоличествоЧеловек;
	НоваяСтрока.СуммаПлатежа = СуммаПлатежа;
									 								 	
КонецПроцедуры


#Область ПодключаемыеКоманды


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
    
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
    
КонецПроцедуры


&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
    
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти


#КонецОбласти













