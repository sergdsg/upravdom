
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
	
Процедура ОбработкаПроведения(Отказ, Режим)
	
   // Проверка, проведен ли текущий документ, для запрета перепрведения	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияПоПлатежам.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.СведенияПоПлатежам КАК СведенияПоПлатежам
		|ГДЕ
		|	СведенияПоПлатежам.Регистратор = &Ссылка
		|	И СведенияПоПлатежам.ВидПлатежа = &ВидПлатежа";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидПлатежа", ВидПлатежа);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = 
		"Повторное проведение документа запрещено. Для изменения необходимо сначала отменить проведение.";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	// Проверка, утановлены ли уже первичные данные.	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УстановкаПервичныхДанныхЭлектроснабжения.Представление КАК Представление
		|ИЗ
		|	Документ.УстановкаПервичныхДанныхЭлектроснабжения КАК УстановкаПервичныхДанныхЭлектроснабжения
		|ГДЕ
		|	УстановкаПервичныхДанныхЭлектроснабжения.Проведен
		|	И УстановкаПервичныхДанныхЭлектроснабжения.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выгрузить()[0];
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Первичные данные электроснабжения уже установлены! Регистратор : "
				 + Выборка.Представление;
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
			
	// регистр СведенияПоПлатежам
	Движения.СведенияПоПлатежам.Записывать = Истина;
	Для Каждого Строка Из НачальныеПоказания Цикл
		ДвижениеСведения = Движения.СведенияПоПлатежам.Добавить();
		ДвижениеСведения.Период = КрайнийОплаченныйПериодСлужебный;
		ДвижениеСведения.ВидПлатежа = ВидПлатежа;
		ДвижениеСведения.ТекущиеПоказания = Строка.КрайниеПереданныеПоказания;
		ДвижениеСведения.ПоказанияНачисления = Строка.КрайниеОплаченныеПоказания;
		ДвижениеСведения.Количество = Строка.Количество;
		ДвижениеСведения.Сумма = Строка.СуммаПлатежа;		
	КонецЦикла;
	
	// регистр накопления ПотреблениеКомунальныхУслуг обороты
	Движения.ПотреблениеКомунальныхУслуг.Записывать = Истина;
	Для Каждого Строка Из НачальныеПоказания Цикл
		ДвижениеПотребление = Движения.ПотреблениеКомунальныхУслуг.Добавить();
		ДвижениеПотребление.Период = КрайнийОплаченныйПериодСлужебный;
		ДвижениеПотребление.ВидПлатежа = ВидПлатежа;
		ДвижениеПотребление.Количество = Строка.Количество;
		ДвижениеПотребление.Сумма = Строка.СуммаПлатежа;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли