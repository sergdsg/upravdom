#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// обязательная проверка на загрузку данных
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	СуммаПоДокументу = ДанныеПлатежа.Итог("СуммаПлатежа");
	
	РаботаСДокументамиСерверУД.УстановкаНомераДокумента(ЭтотОбъект,"ОплачиваемыйПериодСлужебный");
	
КонецПроцедуры

	
Процедура ОбработкаПроведения(Отказ, Режим)
	//проверка, существует ли платеж за этот месяц
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияПоПлатежам.Регистратор.Представление КАК РегистраторПредставление
		|ИЗ
		|	РегистрСведений.СведенияПоПлатежам КАК СведенияПоПлатежам
		|ГДЕ
		|	СведенияПоПлатежам.ВидПлатежа = &ВидПлатежа
		|	И СведенияПоПлатежам.Период = &ОплачиваемыйПериодСлужебный";
	
	Запрос.УстановитьПараметр("ВидПлатежа", ВидПлатежа);
	Запрос.УстановитьПараметр("ОплачиваемыйПериодСлужебный", ОплачиваемыйПериодСлужебный);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выгрузить()[0];
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Платеж за " + ОплачиваемыйМесяц + " " + Формат(ГодПлатежа,"ЧГ=0;") + 
			" года уже проведен. Регистратор: " + Выборка.РегистраторПредставление;
		Сообщение.Сообщить();;	
	    Отказ = Истина;
	КонецЕсли;
	
	Движения.СведенияПоПлатежам.Записывать = Истина;
	Движения.ПотреблениеКомунальныхУслуг.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОплатаИнтернетДанныеПлатежа.ТарифИнтернет КАК ТарифИнтернет,
		|	ОплатаИнтернетДанныеПлатежа.ТарифДопУслугиИнтернет КАК ТарифДопУслугиИнтернет,
		|	ОплатаИнтернетДанныеПлатежа.СуммаПлатежа КАК СуммаПлатежа
		|ИЗ
		|	Документ.ОплатаИнтернет.ДанныеПлатежа КАК ОплатаИнтернетДанныеПлатежа
		|ГДЕ
		|	ОплатаИнтернетДанныеПлатежа.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТарифДопУслугиИнтернет = 0 Тогда
			КоличествоДопУслуг = 0;
		Иначе
			КоличествоДопУслуг = 1;
		КонецЕсли;
		
		КоличествоУслуг = 1 + КоличествоДопУслуг;
		
		// регистр СведенияПоГазоснабжению	
		ДвижениеСведения = Движения.СведенияПоПлатежам.Добавить();
		ДвижениеСведения.Период = ОплачиваемыйПериодСлужебный;
		ДвижениеСведения.ВидПлатежа = ВидПлатежа;
		ДвижениеСведения.Количество = КоличествоУслуг;
		ДвижениеСведения.Тариф = Выборка.ТарифИнтернет;
		ДвижениеСведения.Сумма = Выборка.СуммаПлатежа;
		ДвижениеСведения.ДопТариф = Выборка.ТарифДопУслугиИнтернет;
		
		// регистр ПотреблениеКомунальныхУслуг
		ДвижениеПотребление = Движения.ПотреблениеКомунальныхУслуг.Добавить();
		ДвижениеПотребление.Период = ОплачиваемыйПериодСлужебный;
		ДвижениеПотребление.ВидПлатежа = ВидПлатежа;
		ДвижениеПотребление.Количество = КоличествоУслуг;		
		ДвижениеПотребление.Сумма = Выборка.СуммаПлатежа;
		ДвижениеПотребление.Тариф = Выборка.ТарифИнтернет;
		ДвижениеПотребление.ДопТариф = Выборка.ТарифДопУслугиИнтернет;
	КонецЦикла;
	

КонецПроцедуры


#КонецОбласти


#КонецЕсли