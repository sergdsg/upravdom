
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс


// Переопределяет настройки печати для объекта.
    //
    // Параметры:
    //  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
    //
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
 	
	Настройки.ПриДобавленииКомандПечати = Истина;
           
КонецПроцедуры


 // Заполняет список команд печати.
    // 
    // Параметры:
    //   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
    //
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
        
		Команда = КомандыПечати.Добавить();		
//		Команда.МенеджерПечати = "Документ.ОплатаГазоснабжения"; //Можно не указывать т.к. Мен-р сам документ.
		Команда.Идентификатор = "ПечатьГазоснабжение";
		Команда.Представление = НСтр("ru = 'Печать платежа'");
		Команда.ПроверкаПроведенияПередПечатью = Истина;
		Команда.Порядок = 10;  
		
          
КонецПроцедуры


// Формирует печатные формы.
//
// Параметры:
// Массив - МассивОбъектов - ссылки на объекты, которые нужно распечатать;
// Структура - ПараметрыПечати  - дополнительные настройки печати;
// ТаблицаЗначений - КоллекцияПечатныхФорм - сформированные табличные документы (выходной параметр)
// СписокЗначений - ОбъектыПечати - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
// Структура - ПараметрыВывода - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ПечатьГазоснабжение");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьГазоснабжения(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать оплаты газоснабжения'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ОплатаГазоснабжения.ПФ_MXL_ПечатьГазоснабжение";	    
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	Массив - МассивОбъектов ,  ссылки на объекты, которые нужно распечатать;
//  СписокЗначений - ОбъектыПечати ,  значение  ссылка на объект;
//  представление  имя области, в которой был выведен объект (выходной параметр);
//  
// Возвращаемое значение:
// ТабличныйДокумент - ТабличныйДокумент
Функция ПечатьГазоснабжения(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПечатьГазоснабжение";
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОплатаГазоснабжения.ПФ_MXL_ПечатьГазоснабжение");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОплатаГазоснабжения.Ссылка КАК Ссылка,
		|	ОплатаГазоснабжения.Номер КАК Номер,
		|	ОплатаГазоснабжения.Дата КАК Дата,
		|	ОплатаГазоснабжения.ВидПлатежа КАК ВидПлатежа,
		|	ОплатаГазоснабжения.ОплачиваемыйПериодСлужебный КАК ПериодОплСлужебный,
		|	ОплатаГазоснабжения.Поставщик КАК Поставщик,
		|	ОплатаГазоснабжения.Поставщик.ИНН КАК ПоставщикИНН,
		|	ОплатаГазоснабжения.Поставщик.Телефон КАК ПоставщикТелефон,
		|	ОплатаГазоснабжения.Поставщик.ЭлектроннаяПочта КАК ПоставщикЭлектроннаяПочта,
		|	ОплатаГазоснабжения.Поставщик.Сайт КАК ПоставщикСайт,
		|	ОплатаГазоснабжения.Поставщик.ЛицевойСчет КАК ПоставщикЛицевойСчет,
		|	ОплатаГазоснабжения.Комментарий КАК Комментарий,
		|	ОплатаГазоснабжения.ДанныеПлатежа.(
		|		ТекущиеПоказания КАК ТекущиеПоказания,
		|		ПоказанияНачисления КАК ПоказанияНачисления,
		|		СтарыеПоказания КАК СтарыеПоказания,
		|		Количество КАК Количество,
		|		Тариф КАК Тариф,
		|		СуммаПлатежа КАК СуммаПлатежа) КАК ДанныеПлатежа
		|ИЗ
		|	Документ.ОплатаГазоснабжения КАК ОплатаГазоснабжения
		|ГДЕ
		|	ОплатаГазоснабжения.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();

	РаботаСДокументамиПечатьСерверУД.ВыводНаПечатьОплаты(ТабличныйДокумент, Макет, РезультатЗапроса, ОбъектыПечати);
	
	ТабличныйДокумент.ОриентацияСтраницы =  ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции


#КонецОбласти

#КонецЕсли


