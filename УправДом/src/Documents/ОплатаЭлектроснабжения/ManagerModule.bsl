
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс


// Переопределяет настройки печати для объекта.
    //
    // Параметры:
    //  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
    //
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
 	
	Настройки.ПриДобавленииКомандПечати = Истина;
           
КонецПроцедуры


 // Заполняет список команд печати.
    // 
    // Параметры:
    //   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
    //
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
        
		Команда = КомандыПечати.Добавить();		
//		Команда.МенеджерПечати = "Документ.ОплатаГазоснабжения"; //Можно не указывать т.к. Мен-р сам документ.
		Команда.Идентификатор = "ПечатьЭлектроснабжение";
		Команда.Представление = НСтр("ru = 'Печать платежа'");
		Команда.ПроверкаПроведенияПередПечатью = Истина;
		Команда.Порядок = 10;  
		
          
КонецПроцедуры


// Формирует печатные формы.
//
// Параметры:
// Массив - МассивОбъектов - ссылки на объекты, которые нужно распечатать;
// Структура - ПараметрыПечати  - дополнительные настройки печати;
// ТаблицаЗначений - КоллекцияПечатныхФорм - сформированные табличные документы (выходной параметр)
// СписокЗначений - ОбъектыПечати - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
// Структура - ПараметрыВывода - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ПечатьЭлектроснабжение");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьЭлектроснабжения(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать оплаты электроснабжения'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ОплатаЭлектроснабжения.ПФ_MXL_ПечатьЭлектроснабжение";	    
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	Массив - МассивОбъектов ,  ссылки на объекты, которые нужно распечатать;
//  СписокЗначений - ОбъектыПечати ,  значение  ссылка на объект;
//  представление  имя области, в которой был выведен объект (выходной параметр);
//  
// Возвращаемое значение:
// ТабличныйДокумент - ТабличныйДокумент
Функция ПечатьЭлектроснабжения(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПечатьЭлектроснабжение";
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОплатаЭлектроснабжения.ПФ_MXL_ПечатьЭлектроснабжение");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОплатаЭлектроснабжения.Ссылка КАК Ссылка,
		|	ОплатаЭлектроснабжения.Номер КАК Номер,
		|	ОплатаЭлектроснабжения.Дата КАК Дата,
		|	ОплатаЭлектроснабжения.ВидПлатежа КАК ВидПлатежа,
		|	ОплатаЭлектроснабжения.ОплачиваемыйПериодСлужебный КАК ПериодОплСлужебный,
		|	ОплатаЭлектроснабжения.Поставщик КАК Поставщик,
		|	ОплатаЭлектроснабжения.Поставщик.ИНН КАК ПоставщикИНН,
		|	ОплатаЭлектроснабжения.Поставщик.ЛицевойСчет КАК ПоставщикЛицевойСчет,
		|	ОплатаЭлектроснабжения.Комментарий КАК Комментарий,
		|	ОплатаЭлектроснабжения.ДанныеПлатежа.(
		|		ТекущиеПоказания КАК ТекущиеПоказания,
		|		ПоказанияНачисления КАК ПоказанияНачисления,
		|		СтарыеПоказания КАК СтарыеПоказания,
		|		Количество КАК Количество,
		|		Тариф КАК Тариф,
		|		СуммаПлатежа КАК СуммаПлатежа) КАК ДанныеПлатежа,
		|	ОплатаЭлектроснабжения.Поставщик.КонтактнаяИнформация.(
		|		ВидДляСписка КАК ВидДляСписка,
		|		Представление КАК Представление) КАК КонтактнаяИнформацияПоставщика
		|ИЗ
		|	Документ.ОплатаЭлектроснабжения КАК ОплатаЭлектроснабжения
		|ГДЕ
		|	ОплатаЭлектроснабжения.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();

	РаботаСДокументамиПечатьСерверУД.ВыводНаПечатьОплаты(ТабличныйДокумент, Макет, РезультатЗапроса, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
	
КонецФункции


#КонецОбласти

#КонецЕсли
