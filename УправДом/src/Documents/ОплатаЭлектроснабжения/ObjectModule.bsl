#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ОбработкаПроведения(Отказ, Режим)
	//проверка, существует ли платеж за этот месяц
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияПоПлатежам.Регистратор.Представление КАК РегистраторПредставление
		|ИЗ
		|	РегистрСведений.СведенияПоПлатежам КАК СведенияПоПлатежам
		|ГДЕ
		|	СведенияПоПлатежам.ВидПлатежа = &ВидПлатежа
		|	И СведенияПоПлатежам.Период = &ОплачиваемыйПериодСлужебный";
	
	Запрос.УстановитьПараметр("ВидПлатежа", ВидПлатежа);
	Запрос.УстановитьПараметр("ОплачиваемыйПериодСлужебный", ОплачиваемыйПериодСлужебный);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выгрузить()[0];
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Платеж за " + ОплачиваемыйМесяц + " " + Формат(ГодПлатежа,"ЧГ=0;") + 
			" года уже проведен. Регистратор: " + Выборка.РегистраторПредставление;
		Сообщение.Сообщить();;	
	    Отказ = Истина;
	КонецЕсли;
	
	// регистр СведенияПоГазоснабжению
	Движения.СведенияПоПлатежам.Записывать = Истина;
	Для Каждого Строка Из ДанныеПлатежа Цикл
		ДвижениеСведения = Движения.СведенияПоПлатежам.Добавить();
		ДвижениеСведения.Период = ОплачиваемыйПериодСлужебный;
		ДвижениеСведения.ВидПлатежа = ВидПлатежа;
		ДвижениеСведения.ТекущиеПоказания = Строка.ТекущиеПоказания;
		ДвижениеСведения.ПоказанияНачисления = Строка.ПоказанияНачисления;
		ДвижениеСведения.СтарыеПоказания = Строка.СтарыеПоказания;
		ДвижениеСведения.Количество = Строка.Количество;
		ДвижениеСведения.Тариф = Строка.Тариф;
		ДвижениеСведения.Сумма = Строка.СуммаПлатежа;
	КонецЦикла;
	
	// регистр ПотреблениеКомунальныхУслуг
	Движения.ПотреблениеКомунальныхУслуг.Записывать = Истина;
	Для Каждого Строка Из ДанныеПлатежа Цикл
		ДвижениеПотребление = Движения.ПотреблениеКомунальныхУслуг.Добавить();
		ДвижениеПотребление.Период = ОплачиваемыйПериодСлужебный;
		ДвижениеПотребление.ВидПлатежа = ВидПлатежа;
		ДвижениеПотребление.Количество = Строка.Количество;		
		ДвижениеПотребление.Сумма = Строка.СуммаПлатежа;
		ДвижениеПотребление.Тариф = Строка.Тариф;
	КонецЦикла;
	

КонецПроцедуры

#КонецОбласти

#КонецЕсли