#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс


// Возвращает количество человек, учитываемых в платежах.
//
// Возвращаемое значение:
//  Число - КоличествоЧеловек, учитываемых в платежах.
//
Функция ПолучитьКоличествоЧеловекУчитыватьВПлатежах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(СоставСемьи.Ссылка) КАК КоличествоЧеловек
		|ИЗ
		|	Справочник.СоставСемьи КАК СоставСемьи
		|ГДЕ
		|	СоставСемьи.УчитыватьВПлатежах = ИСТИНА";

	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
		Возврат Выборка.КоличествоЧеловек;
		
КонецФункции


// Переопределяет настройки печати для объекта.
    //
    // Параметры:
    //  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
    //
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
 	
	Настройки.ПриДобавленииКомандПечати = Истина;
           
КонецПроцедуры


 // Заполняет список команд печати.
    // 
    // Параметры:
    //   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
    //
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
        
		Команда = КомандыПечати.Добавить();		
//		Команда.МенеджерПечати = "Документ.ОплатаГазоснабжения"; //Можно не указывать т.к. Мен-р сам документ.
		Команда.Идентификатор = "ПечатьКарточки";
		Команда.Представление = НСтр("ru = 'Печать карточки'");
//		Команда.ПроверкаПроведенияПередПечатью = Истина;
		Команда.Порядок = 10;  
		
          
КонецПроцедуры


// Формирует печатные формы.
//
// Параметры:
// Массив - МассивОбъектов - ссылки на объекты, которые нужно распечатать;
// Структура - ПараметрыПечати  - дополнительные настройки печати;
// ТаблицаЗначений - КоллекцияПечатныхФорм - сформированные табличные документы (выходной параметр)
// СписокЗначений - ОбъектыПечати - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
// Структура - ПараметрыВывода - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ПечатьКарточки");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьКарточки(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать карточки домочадца'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Справочник.СоставСемьи.ПФ_MXL_ПечатьКарточки";	    
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


// Печать карточки.
// 
// Параметры:
// Массив - МассивОбъектов - Массив объектов
// СписокЗначений -  ОбъектыПечати - СписокЗначений Объекты печати
// 
// Возвращаемое значение:
// ТабличныйДокумент
  
Функция ПечатьКарточки(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
//	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПечатьКарточки";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.СоставСемьи.ПФ_MXL_ПечатьКарточки");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставСемьи.Ссылка КАК Ссылка,
		|	СоставСемьи.Код КАК Код,
		|	СоставСемьи.Наименование КАК ПолноеИмя,
		|	СоставСемьи.СемейныйСтатус КАК СемейныйСтатус,
		|	СоставСемьи.ДатаРождения КАК ДатаРождения,
		|	СоставСемьи.Пол КАК Пол,
		|	СоставСемьи.УчитыватьВПлатежах КАК УчитыватьВПлатежах,
		|	СоставСемьи.ФайлКартинки КАК ФайлКартинки,
		|	СоставСемьи.Комментарий КАК Комментарий,
		|	СоставСемьи.ЛичныеДокументы.(
		|		НомерСтроки КАК НомерСтроки,
		|		Документ КАК Документ,
		|		Серия КАК Серия,
		|		Номер КАК Номер,
		|		ДатаВыдачи КАК ДатаВыдачи,
		|		КемВыдан КАК КемВыдан) КАК ЛичныеДокументы,
		|	СоставСемьи.КонтактнаяИнформация.(
		|		ВидДляСписка КАК ВидДляСписка,
		|		Представление КАК Представление) КАК КонтактнаяИнформация
		|ИЗ
		|	Справочник.СоставСемьи КАК СоставСемьи
		|ГДЕ
		|	СоставСемьи.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаДокументы = Макет.ПолучитьОбласть("ШапкаДокументы");
	ОбластьСтрокаДокументы = Макет.ПолучитьОбласть("СтрокаДокументы");
	ОбластьКоммент = Макет.ПолучитьОбласть("Коммент");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	СписокПолей = "";
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		СписокПолей = СписокПолей + ?(СписокПолей = "","",",") + Колонка.Имя;
	КонецЦикла;
	
	ТабличныйДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
	
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
//	Вывод документа на печать
	
	//Область Шапка 
	
	Если Не Выборка.ФайлКартинки.Пустая() Тогда
		ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(Выборка.ФайлКартинки,Ложь);
		
		Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда 
			КартинкаФото = Новый Картинка(ДвоичныеДанные);
			ОбластьШапка.Рисунки.ФотоДомочадца.Картинка = КартинкаФото;	
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеЗаполненияШапка = Новый Структура(СписокПолей);
	ЗаполнитьЗначенияСвойств(ДанныеЗаполненияШапка, Выборка);
	ДанныеЗаполненияШапка.Вставить("ДатаЗаголовок",Формат(ТекущаяДатаСеанса(),"ДЛФ=DD;" ));
	ВозрастПредставление = РаботаСДатамиКлиентСерверУД.РассчитатьВозраст(ДанныеЗаполненияШапка.ДатаРождения);
	ДанныеЗаполненияШапка.Вставить("ВозрастПредставление", ВозрастПредставление);
	ОбластьШапка.Параметры.Заполнить(ДанныеЗаполненияШапка);				
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	//Область ШапкаДокументы		
	ТабличныйДокумент.Вывести(ОбластьШапкаДокументы);
	
	//ОбластьСтрокаДокументы
	РезультатДокументы = Выборка.ЛичныеДокументы;
	СписокПолейСтроки = "";
	Для Каждого Колонка Из РезультатДокументы.Колонки Цикл
		СписокПолейСтроки = СписокПолейСтроки + ?(СписокПолейСтроки = "","",",") + Колонка.Имя;
	КонецЦикла;
	
	ВыборкаДокументы = РезультатДокументы.Выбрать();
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ДанныеЗаполненияСтрока = Новый Структура(СписокПолейСтроки);
		ЗаполнитьЗначенияСвойств(ДанныеЗаполненияСтрока, ВыборкаДокументы);
		ОбластьСтрокаДокументы.Параметры.Заполнить(ДанныеЗаполненияСтрока); 
		ТабличныйДокумент.Вывести(ОбластьСтрокаДокументы);
	КонецЦикла;
		
	//ОбластьКоммент
	Если Не ДанныеЗаполненияШапка.Комментарий = "" Тогда
		
		ОбластьКоммент.Параметры.Заполнить(ДанныеЗаполненияШапка);
		ТабличныйДокумент.Вывести(ОбластьКоммент);
		
	КонецЕсли;
		
	//ОбластьПодвал	
	ДанныеЗаполненияПодвал = Новый Структура;
	
	// Заполнение контактной информации поставщика
		РезультатКонтактнаяИнформация = Выборка.КонтактнаяИнформация;
		СтруктураКИ = РаботаСДокументамиПечатьСерверУД.КонтактнаяИнформацияДляПечати(РезультатКонтактнаяИнформация);		
		СтруктураВидаКИ = СтруктураКИ.ВидыКИ;
		СтруктураПредставленияКИ = СтруктураКИ.ПредставленияКИ;
		
		Для Каждого Элемент Из СтруктураВидаКИ Цикл
			ДанныеЗаполненияПодвал.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
		
		Для Каждого Элемент Из СтруктураПредставленияКИ Цикл
			ДанныеЗаполненияПодвал.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
	
	ДанныеЗаполненияПодвал.Вставить("Адрес",Константы.АдресУД.Получить());
	ОбластьПодвал.Параметры.Заполнить(ДанныеЗаполненияПодвал);
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
//	Конец Вывода	
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
								НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции


Функция ПолучитьПодписиИПечати(Плательщик) Экспорт

	НаборКартинок = Новый Структура;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СоставСемьи.ФайлПодписи, ЗНАЧЕНИЕ(Справочник.СоставСемьиПрисоединенныеФайлы.ПустаяСсылка)) КАК
		|		ФайлПодписи,
		|	ЕСТЬNULL(СоставСемьи.ФайлПечати, ЗНАЧЕНИЕ(Справочник.СоставСемьиПрисоединенныеФайлы.ПустаяСсылка)) КАК
		|		ФайлПечати
		|ИЗ
		|	Справочник.СоставСемьи КАК СоставСемьи
		|ГДЕ
		|	СоставСемьи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Плательщик);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		КартинкаПечать = КартинкаИзФайла(Выборка.ФайлПечати);
		НаборКартинок.Вставить("ПечатьПлательщика",КартинкаПечать);
		
		КартинкаПодпись = КартинкаИзФайла(Выборка.ФайлПодписи);
		НаборКартинок.Вставить("ПодписьПлательщика",КартинкаПодпись);
		
	Иначе 
		НаборКартинок.Вставить("ПечатьПлательщика",Новый Картинка);
		НаборКартинок.Вставить("ПодписьПлательщика",Новый Картинка);		
	КонецЕсли;;
	
	Возврат НаборКартинок;

КонецФункции


Функция КартинкаИзФайла(Файл)
	
	Если ЗначениеЗаполнено(Файл) Тогда
		ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(Файл, Ложь);
	КонецЕсли;

	Если ДвоичныеДанные = Неопределено Тогда
		Возврат Новый Картинка;
	КонецЕсли;
	
	Возврат Новый Картинка(ДвоичныеДанные,Истина);
	
КонецФункции


#КонецОбласти


#КонецЕсли




