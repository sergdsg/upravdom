

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    // СтандартныеПодсистемы.КонтактнаяИнформация
    ДополнительныеПараметры = УправлениеКонтактнойИнформацией.ПараметрыКонтактнойИнформации();
    ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаКонтактнаяИнформация");
    УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, ДополнительныеПараметры);
    // Конец СтандартныеПодсистемы.КонтактнаяИнформация
    
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
    
    // СтандартныеПодсистемы.КонтактнаяИнформация
    УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.КонтактнаяИнформация
    
КонецПроцедуры


&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
    
    // СтандартныеПодсистемы.КонтактнаяИнформация
    УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
    // Конец СтандартныеПодсистемы.КонтактнаяИнформация
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    
    // СтандартныеПодсистемы.КонтактнаяИнформация
    УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.КонтактнаяИнформация
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
    
    // СтандартныеПодсистемы.КонтактнаяИнформация
    УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.КонтактнаяИнформация
    
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Асинх Процедура ЗаполнитьПоИНН(Команда)
	
	ИНН = Ждать ВвестиСтрокуАсинх(,"Введите ИНН", 10);

	Если ИНН = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(ИНН)<> 10 Тогда	
		ПредупреждениеАсинх("Некорректный ИНН... Повторите ввод данных.");
		Возврат;	
	КонецЕсли;
	
	ДанныеПоставщика = ПолучитьДанныеПоставщикаПоИНН(ИНН);
	
	ЗаполнитьЗначенияСвойств(Объект, ДанныеПоставщика);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоставщикаПоИНН(ИНН)
	
	ДанныеПоставщика = Новый Структура;
	ДанныеПоставщика.Вставить("ИНН",ИНН);
	
//	https://egrul.itsoft.ru/short_data/?3445045202
	
	АдресСервера = "egrul.itsoft.ru";
	АдресРесурса = "/short_data/?" + ИНН;
	
	Соединение = Новый HTTPСоединение(АдресСервера, , , , ,30, Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос(АдресРесурса);
	
	Попытка
		
		Ответ = Соединение.Получить(Запрос);
		
	Исключение
		Сообщение  = Новый СообщениеПользователю();
		Сообщение.Текст = "Не удалось получить данные по ИНН " + ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат ДанныеПоставщика;
	КонецПопытки;
	
	СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();	
	ДанныеЮЛ = Неопределено;
	
	Если Ответ.КодСостояния = 200 Тогда
			
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		
		ДанныеЮЛ = ПрочитатьJSON(ЧтениеJSON,Истина);
		
	Иначе
		ТекстСообщения = СтрШаблон("Не удалось получить данные по ИНН...
									|Код состояния: %1
									|Ответ сервера: %2",Ответ.КодСостояния,СтрокаJSON);
		Сообщение  = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();							
		Возврат ДанныеПоставщика;								
	КонецЕсли;
	
	ДанныеПоставщика.Вставить("Наименование", ДанныеЮЛ["short_name"]);
	ДанныеПоставщика.Вставить("ПолноеИмя", ДанныеЮЛ["full_name"]);
	ДанныеПоставщика.Вставить("ОГРН", ДанныеЮЛ["ogrn"]);
	ДанныеПоставщика.Вставить("КПП", ДанныеЮЛ["kpp"]);
	
	Возврат ДанныеПоставщика;
	
КонецФункции


#Область КонтактнаяИнформация


// СтандартныеПодсистемы.КонтактнаяИнформация
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
        УправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
    УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
    УправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
    УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
    
    УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
    
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
    
    УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
    
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
    УправлениеКонтактнойИнформациейКлиент.НачатьОбработкуНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
    ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры


&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
    УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры
// Конец СтандартныеПодсистемы.КонтактнаяИнформация


#КонецОбласти


#КонецОбласти
