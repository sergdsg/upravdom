


#Область СлужебныеПроцедурыИФункции

// РаботаСПрисоединеннымиФайламиКлиентСервер

// Общая процедура для создания стандартных групп присоединенных файлов для любого объекта.
// Используется в формах объектов для автоматического создания групп.
//
// Параметры:
//  ВладелецФайлов        - СправочникСсылка, ДокументСсылка - Объект, для которого создаются группы.
//  ИмяСправочникаФайлов  - Строка - Полное имя справочника присоединенных файлов (например, "Документ1ПрисоединенныеФайлы").
//  СоздаваемыеГруппы     - Массив из Строка - Массив наименований групп, которые необходимо создать.
//
Процедура СоздатьСтандартныеГруппыПрисоединенныхФайлов(ВладелецФайлов, ИмяСправочникаФайлов, СоздаваемыеГруппы) Экспорт
	
	// Проверка входящих параметров
	Если ВладелецФайлов.Пустая() ИЛИ Не ЗначениеЗаполнено(ИмяСправочникаФайлов) Или СоздаваемыеГруппы = Неопределено Или СоздаваемыеГруппы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем, не созданы ли уже группы для этого владельца
	Если ГруппыПрисоединенныхФайловСуществуют(ВладелецФайлов, ИмяСправочникаФайлов, СоздаваемыеГруппы) Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем менеджер справочника по его имени
	Попытка
		МенеджерФайлов = Справочники[ИмяСправочникаФайлов];
	Исключение
		// Если справочник с таким именем не найден, просто выходим, чтобы не вызвать ошибку.
		Возврат;
	КонецПопытки;
	
	// Создаем группы
	Для Каждого ИмяГруппы Из СоздаваемыеГруппы Цикл
		
		НоваяГруппа = МенеджерФайлов.СоздатьГруппу();
		НоваяГруппа.Наименование = ИмяГруппы;
		НоваяГруппа.Родитель = МенеджерФайлов.ПустаяСсылка();
		НоваяГруппа.ВладелецФайла = ВладелецФайлов;
//		НоваяГруппа.ДатаМодификацииУниверсальная = ТекущаяУниверсальнаяДата();
				
		НоваяГруппа.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Вспомогательная функция для проверки существования групп
//
Функция ГруппыПрисоединенныхФайловСуществуют(ВладелецФайлов, ИмяСправочникаФайлов, ПроверяемыеГруппы)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПрисоединенныеФайлы.Ссылка
		|ИЗ
		|	Справочник.%1 КАК ПрисоединенныеФайлы
		|ГДЕ
		|	ПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайлов
		|	И ПрисоединенныеФайлы.Наименование В (&СписокГрупп)
		|	И ПрисоединенныеФайлы.ЭтоГруппа = ИСТИНА";
	
	// Подставляем имя справочника в текст запроса
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяСправочникаФайлов);
	
	Запрос.УстановитьПараметр("ВладелецФайлов", ВладелецФайлов);
	Запрос.УстановитьПараметр("СписокГрупп", ПроверяемыеГруппы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции



#КонецОбласти
