


#Область СлужебныеПроцедурыИФункции


Процедура ВыводНаПечатьОплаты(ТабличныйДокумент, Макет, РезультатЗапроса, ОбъектыПечати) Экспорт
	
	Выборка = РезультатЗапроса.Выбрать(); 

	
	ТабличныйДокумент.Очистить();
	
	//	Формируем список ключей для структуры ДанныеЗаполненияШапка
	СписокПолей = "";
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		СписокПолей = СписокПолей + ?(СписокПолей = "","",",") + Колонка.Имя;	
	КонецЦикла;
	
	//	многостраничная печать
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
	
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	
		//	ОбластьШапка
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ДанныеЗаполненияШапка = Новый Структура(СписокПолей);
		ЗаполнитьЗначенияСвойств(ДанныеЗаполненияШапка, Выборка);
		
		//	Получаем стуктуру для заполнения полей Счетчика в шапке	
		ДанныеЗаполненияСчетчик = РаботаСДокументамиСерверУД.ПолучитьДанныеДляЗаполненияШапки(Выборка.ВидПлатежа);
		// дозаполняем 	ДанныеЗаполненияШапка из структуры ДанныеЗаполненияСчетчик	
		Для Каждого Элемент Из ДанныеЗаполненияСчетчик Цикл
			ДанныеЗаполненияШапка.Вставить(Элемент.Ключ,Элемент.Значение);
		КонецЦикла;
		
		// Заполнение контактной информации поставщика
		РезультатКонтактнаяИнформация = Выборка.КонтактнаяИнформацияПоставщика;
		СтруктураКИ = КонтактнаяИнформацияДляПечати(РезультатКонтактнаяИнформация);		
		СтруктураВидаКИ = СтруктураКИ.ВидыКИ;
		СтруктураПредставленияКИ = СтруктураКИ.ПредставленияКИ;
		
		Для Каждого Элемент Из СтруктураВидаКИ Цикл
			ДанныеЗаполненияШапка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
		
		Для Каждого Элемент Из СтруктураПредставленияКИ Цикл
			ДанныеЗаполненияШапка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
		
        // Форматирум дату и убираем нули для заголовка документа		
		ДанныеЗаполненияШапка.Вставить("ДатаЗаголовок",Формат(Выборка.Дата,"ДЛФ=DD;"));
		ДанныеЗаполненияШапка.Вставить("НомерЗаголовок",ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер));
		
		ОбластьШапка.Параметры.Заполнить(ДанныеЗаполненияШапка);
		ТабличныйДокумент.Вывести(ОбластьШапка);
			
		//	ОбластьДанныеПлатежаШапка
		Если Выборка.ВидПлатежа = Справочники.ВидыПлатежей.Интернет 
									И Не Выборка.УчитыватьДопыИнтернет Тогда			
			ОбластьДанныеПлатежаШапка = Макет.ПолучитьОбласть("ШапкаДанныеПлатежа2");			
		Иначе
			ОбластьДанныеПлатежаШапка = Макет.ПолучитьОбласть("ШапкаДанныеПлатежа");
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьДанныеПлатежаШапка);

		//	ОбластьСтрокаДанныеПлатежа
		
		Если Выборка.ВидПлатежа = Справочники.ВидыПлатежей.Интернет 
												И Не Выборка.УчитыватьДопыИнтернет Тогда					 
			ОбластьСтрокаДанныеПлатежа = Макет.ПолучитьОбласть("СтрокаДанныеПлатежа2");							
		Иначе
			ОбластьСтрокаДанныеПлатежа = Макет.ПолучитьОбласть("СтрокаДанныеПлатежа");	
		КонецЕсли;
			
		
		РезультатДанныеПлатежа = Выборка.ДанныеПлатежа;
		//	Формируем список ключей для структуры ДанныеЗаполненияСтроки
		СписокПолейСтроки = "";
		Для Каждого Колонка Из РезультатДанныеПлатежа.Колонки Цикл
		СписокПолейСтроки = СписокПолейСтроки + ?(СписокПолейСтроки = "","",",") + Колонка.Имя;	
		КонецЦикла;
		
		ВыборкаДанныеПлатежа = РезультатДанныеПлатежа.Выбрать();
		
		Пока ВыборкаДанныеПлатежа.Следующий() Цикл
								
			ДанныеЗаполненияСтроки = Новый Структура(СписокПолейСтроки);
			ЗаполнитьЗначенияСвойств(ДанныеЗаполненияСтроки, ВыборкаДанныеПлатежа);
			ОбластьСтрокаДанныеПлатежа.Параметры.Заполнить(ДанныеЗаполненияСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрокаДанныеПлатежа);
			
		КонецЦикла;
		
		//	ОбластьПодвал
		Если ДанныеЗаполненияШапка.Комментарий <> "" Тогда	
			ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
			ОбластьПодвал.Параметры.Заполнить(ДанныеЗаполненияШапка);
			ТабличныйДокумент.Вывести(ОбластьПодвал);	
		КонецЕсли;
		
		//	ОбластьПлательщик
		ОбластьПлательщик = Макет.ПолучитьОбласть("Плательщик");
		
		ДанныеЗаполненияПлательщик = Новый Структура;
		ДанныеЗаполненияПлательщик.Вставить("Адрес", Константы.АдресУД.Получить());
		ДанныеЗаполненияПлательщик.Вставить("Собственник", Константы.СобственникУД.Получить());
		
		ОбластьПлательщик.Параметры.Заполнить(ДанныеЗаполненияПлательщик);
		ТабличныйДокумент.Вывести(ОбластьПлательщик);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
	
КонецПроцедуры


// Контактная информация для печати.
// 
// Параметры:
//  КонтактаяИнформация - РезультатЗапроса
// 
// Возвращаемое значение:
//  Структура - Контактная информация поставщика для печати:
// * ВидыКИ - Структура  
// * ПредставленияКИ - Структура  
Функция КонтактнаяИнформацияДляПечати(КонтактаяИнформация) Экспорт
	
	СтруктураВидаКИ = Новый Структура;
	СтруктураПредставленияКИ = Новый Структура;
	СтруктураКИ = Новый Структура;
	
	Выборка  = КонтактаяИнформация.Выбрать();
	
	Шаг = 0;
	Пока Выборка.Следующий() Цикл
		СтруктураВидаКИ.Вставить("ИмяКИ_" + Строка(Шаг), Выборка.ВидДляСписка);
		СтруктураПредставленияКИ.Вставить("Представление_" + Строка(Шаг), Выборка.Представление);
		Шаг = Шаг + 1;
	КонецЦикла;
	
	СтруктураКИ.Вставить("ВидыКИ", СтруктураВидаКИ);
	СтруктураКИ.Вставить("ПредставленияКИ", СтруктураПредставленияКИ);
	
	Возврат СтруктураКИ;
	
КонецФункции


#КонецОбласти
