

#Область СлужебныйПрограммныйИнтерфейс

Функция РассчитатьСлужебнуюДату(ВыбранныйМесяц, ВыбранныйГод) Экспорт
	
	// Проверяем, что все данные заполнены
	Если ВыбранныйМесяц = Неопределено ИЛИ ВыбранныйГод = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НомерМесяца = 0;
	

	Если ВыбранныйМесяц = Перечисления.МесяцыОплаты.Январь Тогда
		НомерМесяца = 1;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Февраль Тогда
		НомерМесяца = 2;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Март Тогда
		НомерМесяца = 3;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Апрель Тогда
		НомерМесяца = 4;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Май Тогда
		НомерМесяца = 5;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Июнь Тогда
		НомерМесяца = 6;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Июль Тогда
		НомерМесяца = 7;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Август Тогда
		НомерМесяца = 8;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Сентябрь Тогда
		НомерМесяца = 9;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Октябрь Тогда
		НомерМесяца = 10;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Ноябрь Тогда
		НомерМесяца = 11;
	ИначеЕсли ВыбранныйМесяц = Перечисления.МесяцыОплаты.Декабрь Тогда
		НомерМесяца = 12;
	КонецЕсли;
	
	// Формируем и возвращаем дату
	Возврат Дата(ВыбранныйГод, НомерМесяца, 1);
	
	
КонецФункции

// Функция заполняет реквизиты шапки документа на основе вида платежа.
//
// Параметры:
//  ВидПлатежа - СправочникСсылка.ВидыПлатежей - Вид платежа, по которому нужно получить данные.
//
// Возвращаемое значение:
// Неопределено, Структура - Содержит заполненные реквизиты. Если данные не найдены, возвращается Неопределено.
Функция ПолучитьДанныеДляЗаполненияШапки(Знач ВидПлатежа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыПлатежей.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВидыПлатежей.Счетчик КАК Счетчик,
		|	ВидыПлатежей.Счетчик.Номер КАК СчетчикНомер,
		|	ВидыПлатежей.Счетчик.ДатаУстановки КАК СчетчикДатаУстановки
		|ИЗ
		|	Справочник.ВидыПлатежей КАК ВидыПлатежей
		|ГДЕ
		|	ВидыПлатежей.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ВидПлатежа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		 Выборка = РезультатЗапроса.Выбрать();	
		 Выборка.Следующий();
		 
		 // Формируем структуру с результатами
		 Результат = Новый Структура;
		 Результат.Вставить("ЕдиницаИзмерения", Выборка.ЕдиницаИзмерения);
		 Результат.Вставить("Счетчик", Выборка.Счетчик);
		 Результат.Вставить("НомерСчетчика", Выборка.СчетчикНомер);
		 Результат.Вставить("ДатаУстановки", Выборка.СчетчикДатаУстановки);
		 
		 Возврат Результат;
	Иначе 
		 // Если данные не найдены, возвращаем Неопределено
		 Возврат Неопределено;
	 КонецЕсли;
	 	
КонецФункции


// Функция получает данные о показаниях из записи за предыдущий месяц.
// Использует СрезПоследних для максимальной производительности.
//
// Параметры:
//  ВидПлатежа - СправочникСсылка.ВидыПлатежей - Вид платежа.
//  ОплачиваемыйПериод - Дата - Дата в текущем месяце, за который формируется документ.
//
// Возвращаемое значение:
//  Неопределено,Структура - Содержит ПоказанияНачисления и СтарыеПоказания.
//              Если данные за прошлый месяц не найдены, возвращает Неопределено.
//
Функция ПолучитьПоказанияЗаПрошлыйМесяц(Знач ВидПлатежа, ОплачиваемыйПериод) Экспорт
	
	// 1. Определяем границы ПРОШЛОГО месяца. 
	// Это пока избыточно. Но в будущем, если в месяце будет несколько записей.
	НачалоПрошлогоМесяца = НачалоМесяца(ДобавитьМесяц(ОплачиваемыйПериод, -1));
	КонецПрошлогоМесяца = КонецМесяца(НачалоПрошлогоМесяца);
	
	// 2. Запрос к регистру сведений
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияПоПлатежам.ТекущиеПоказания КАК ТекущиеПоказания,
		|	СведенияПоПлатежам.ПоказанияНачисления КАК ПоказанияНачисления
		|ИЗ
		|	РегистрСведений.СведенияПоПлатежам КАК СведенияПоПлатежам
		|ГДЕ
		|	СведенияПоПлатежам.ВидПлатежа = &ВидПлатежа
		|	И СведенияПоПлатежам.Период МЕЖДУ &НачалоПрошлогоМесяца И &КонецПрошлогоМесяца
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ"; // Берем последнюю запись в месяце, если их было несколько
	
	Запрос.УстановитьПараметр("ВидПлатежа", ВидПлатежа);
	Запрос.УстановитьПараметр("НачалоПрошлогоМесяца", НачалоПрошлогоМесяца);
	Запрос.УстановитьПараметр("КонецПрошлогоМесяца", КонецПрошлогоМесяца);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// 3. Получаем данные и формируем структуру
	Выборка = РезультатЗапроса.Выбрать();	
	Выборка.Следующий();
	
	ДанныеПоказаний = Новый Структура;
	ДанныеПоказаний.Вставить("ПоказанияНачисления", Выборка.ТекущиеПоказания);
	ДанныеПоказаний.Вставить("СтарыеПоказания", Выборка.ПоказанияНачисления); // Берем поле из регистра
	
	Возврат ДанныеПоказаний;

КонецФункции


// Описание: Обработчик проверки заполнения для документов: Газоснабжение, Электроснабжение.
//          Вызывается из подписки на событие ОплатаПроверкаЗаполнения_УД.
Процедура ПроверитьЗаполнениеДокументаОплатаГазСвет(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт

	// Источник - это объект документа, который проводится (ОплатаГазоснабженияОбъект)

	Для Каждого Строка Из Источник.ДанныеПлатежа Цикл
		Если Строка.ТекущиеПоказания < Строка.ПоказанияНачисления Тогда
			
			// Формируем понятное сообщение для пользователя
			Сообщение = Новый СообщениеПользователю;
			ТекстСообщения = СтрШаблон(
    		"Текущие показания (%1) меньше показаний для начисления (%2).",
    		 Строка.ТекущиеПоказания, Строка.ПоказанияНачисления); 			 
			Сообщение.Текст = ТекстСообщения;
			// Указываем, где именно ошибка
			Сообщение.Поле = "ДанныеПлатежа";
			Сообщение.УстановитьДанные(Источник);
			Сообщение.Сообщить();
			
			// Устанавливаем отказ в проведении
			Отказ = Истина;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


// Заполнение контактной информации поствщика на форме документа.
// 
// Параметры:
//  Форма - Форма
//  ПоставщикСсылка - СправочникСсылка
Процедура ЗаполнениеКонтактнойИнформации(Форма, ПоставщикСсылка) Экспорт
	
	//	Для полного удаления элементов контактной информации,
	//	удаляем их с конца
	ИндексУдаления = Форма.Элементы.ГруппаКонтактнаяИнформация.ПодчиненныеЭлементы.Количество() - 1;
	Пока ИндексУдаления >= 0 Цикл
		ЭлементДляУдаления = Форма.Элементы.ГруппаКонтактнаяИнформация.ПодчиненныеЭлементы[ИндексУдаления];	
		Форма.Элементы.Удалить(ЭлементДляУдаления);	
		ИндексУдаления = ИндексУдаления - 1;
	КонецЦикла;
	
	// Очищаем наш реквизит - контейнер
	Форма.КонтактнаяИнформацияПоставщика.Очистить();
	
	// Если поставщик не выбран, ничего больше не делаем
	Если ПоставщикСсылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем контактную информацию поставщика, 
	// через запрос к ТЧ КонтактнаяИнформация Справочника Поставщики
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.ВидДляСписка КАК Вид,
		|	КонтактнаяИнформация.Представление КАК Представление
		|ИЗ
		|	Справочник.Поставщики.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ПоставщикСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Загружаем результат прямо в реквизит формы
	Форма.КонтактнаяИнформацияПоставщика.Загрузить(РезультатЗапроса.Выгрузить());
	
	// создаем элементы и привязываем их к НАШЕМУ реквизиту
	КоличествоКИ = Форма.КонтактнаяИнформацияПоставщика.Количество();
	
	Для Шаг = 0 По КоличествоКИ - 1 Цикл 
		
		НовыйЭлемент = Форма.Элементы.Добавить("ИмяКИ_" + Строка(Шаг), Тип("ПолеФормы"), Форма.Элементы.ГруппаКонтактнаяИнформация);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
		НовыйЭлемент.ПутьКДанным = "КонтактнаяИнформацияПоставщика[" + Строка(Шаг) + "].Представление"; 
		НовыйЭлемент.Заголовок = Форма.КонтактнаяИнформацияПоставщика[Шаг].Вид;
	
	КонецЦикла;
	
КонецПроцедуры


Процедура УстановкаНомераДокумента(ОбъектДокумента, ИмяРеквизитаПериода) Экспорт
	
	// Не меняем номер у уже существующих документов.
	Если НЕ ОбъектДокумента.ЭтоНовый() И НЕ ПустаяСтрока(ОбъектДокумента.Номер) Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем период из реквизита, имя которого нам передали.
	Период = ОбъектДокумента[ИмяРеквизитаПериода];
	
	// Проверка, что период указан корректно.
	Если Период = Дата(1, 1, 1) Тогда
			
		ВызватьИсключение "Запись отменена. Оплачивемый период не указан или указан не полностью.";

	КонецЕсли;
	
	// Вычисляем и присваиваем новый номер.
	СледующийНомер = ПолучитьСледующийНомерДляДокумента(ОбъектДокумента.Метаданные(), Период);
	ОбъектДокумента.Номер = Формат(СледующийНомер, "ЧЦ=9; ЧВН=; ЧГ=");
	
КонецПроцедуры



Функция ПолучитьСледующийНомерДляДокумента(МетаДокумент, Период) Экспорт
	
	НачалоГода = НачалоГода(Период);
	КонецГода = КонецГода(Период);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(Док.Номер) КАК ПоследнийНомер
		|ИЗ
		|	Документ." + МетаДокумент.Имя + " КАК Док
		|ГДЕ
		|	Док.ОплачиваемыйПериодСлужебный МЕЖДУ &НачалоГода И &КонецГода";
		
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода);
	Запрос.УстановитьПараметр("КонецГода", КонецГода);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
		
	ПоследнийНомер = 0;
	Если Выборка.Следующий() Тогда
		Если  Не Выборка.ПоследнийНомер = NULL И Не ПустаяСтрока(Выборка.ПоследнийНомер) Тогда
			ПоследнийНомер = Число(Выборка.ПоследнийНомер);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПоследнийНомер + 1;
	
КонецФункции


#КонецОбласти
