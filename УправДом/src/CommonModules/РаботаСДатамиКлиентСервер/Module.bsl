

#Область СлужебныйПрограммныйИнтерфейс

// Функция для вычисления возраста в формате "45 лет 7 месяцев 18 дней"
// 
Функция РассчитатьВозраст(ДатаРождения) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаРождения) Тогда
		Возврат "";
	КонецЕсли;
	
	РасчетнаяДата = НачалоДня(ДатаРождения);
	ТекущаяДатаБезВремени = НачалоДня(ТекущаяДата());
	
	Если РасчетнаяДата > ТекущаяДатаБезВремени Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Дата рождения не может быть в будущем";
		Сообщение.Поле = "Объект.ДатаРождения";
		Сообщение.Сообщить();	
		Возврат "Установлена некорректная дата рождения";
	КонецЕсли;
	
	Годы = 0;
	Пока ДобавитьМесяц(РасчетнаяДата, 12) <= ТекущаяДатаБезВремени Цикл
		РасчетнаяДата = ДобавитьМесяц(РасчетнаяДата, 12);
		Годы = Годы + 1;
	КонецЦикла;
	
	Месяцы = 0;
	Пока ДобавитьМесяц(РасчетнаяДата, 1) <= ТекущаяДатаБезВремени Цикл
		РасчетнаяДата = ДобавитьМесяц(РасчетнаяДата, 1);
		Месяцы = Месяцы + 1;
	КонецЦикла;
	
	Дни = (ТекущаяДатаБезВремени - РасчетнаяДата) / 86400;
	
	// Формируем строку
	СтрокаВозраст = "";
	Если Годы > 0 Тогда
		СтрокаВозраст = Строка(Годы) + " " + СклонениеЛет(Годы) + " ";
	КонецЕсли;
	Если Месяцы > 0 Тогда
		СтрокаВозраст = СтрокаВозраст + Строка(Месяцы) + " " + СклонениеМесяцев(Месяцы) + " ";
	КонецЕсли;
	Если Дни > 0 Тогда
		СтрокаВозраст = СтрокаВозраст + Строка(Дни) + " " + СклонениеДней(Дни);
	КонецЕсли;
	
	Возврат СокрЛП(СтрокаВозраст);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


// Вспомогательные функции для склонения 
// Вспомогательная функция для склонения слова "год"
Функция СклонениеЛет(Число)
	Если (Число % 100 = 11) ИЛИ (Число % 100 = 12) ИЛИ (Число % 100 = 13) ИЛИ (Число % 100 = 14) Тогда
		Возврат "лет";
	КонецЕсли;
	
	Возврат ?(Число % 10 = 1, "год",
		?(Число % 10 = 2 ИЛИ Число % 10 = 3 ИЛИ Число % 10 = 4, "года", "лет"));
КонецФункции


// Вспомогательная функция для склонения слова "месяц"
Функция СклонениеМесяцев(Число)
	Если (Число % 100 = 11) ИЛИ (Число % 100 = 12) ИЛИ (Число % 100 = 13) ИЛИ (Число % 100 = 14) Тогда
		Возврат "месяцев";
	КонецЕсли;
	
	Возврат ?(Число % 10 = 1, "месяц",
		?(Число % 10 = 2 ИЛИ Число % 10 = 3 ИЛИ Число % 10 = 4, "месяца", "месяцев"));
КонецФункции


// Вспомогательная функция для склонения слова "день"
Функция СклонениеДней(Число)
	Если (Число % 100 = 11) ИЛИ (Число % 100 = 12) ИЛИ (Число % 100 = 13) ИЛИ (Число % 100 = 14) Тогда
		Возврат "дней";
	КонецЕсли;
	
	Возврат ?(Число % 10 = 1, "день",
		?(Число % 10 = 2 ИЛИ Число % 10 = 3 ИЛИ Число % 10 = 4, "дня", "дней"));
КонецФункции


#КонецОбласти
